group 'me.bramhaag'
version '1.1.2-SNAPSHOT'

apply plugin: 'java'

subprojects {
    group 'me.bramhaag'
    version '1.1.3-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'maven'

    repositories {
        mavenCentral()
    }

    dependencies {
        compile 'com.squareup.retrofit2:retrofit:2.2.0'
        compile 'com.squareup.retrofit2:converter-scalars:2.2.0'
        compile 'com.squareup.retrofit2:converter-gson:2.2.0'
        compile 'com.google.code.gson:gson:2.8.0'
        compile 'org.apache.tika:tika-core:1.14'

        compileOnly "org.projectlombok:lombok:1.16.14"
    }

    sourceSets {
        main {
            java {
                srcDir 'src'
            }
        }
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from sourceSets.main.allJava
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar

        archives javadocJar
        archives sourcesJar
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                repository(url: "${nexusUrl}/repository/maven-releases/") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }
                snapshotRepository(url: "${nexusUrl}/repository/maven-snapshots") {
                    authentication(userName: nexusUsername, password: nexusPassword)
                }

                addFilter('jre7') { artifact, file -> artifact.name == 'jre7' }
                addFilter('jre8') { artifact, file -> artifact.name == 'jre8' }

                pom('jre7').artifactId = 'owo-java-jre7'
                pom('jre8').artifactId = 'owo-java-jre8'
            }
        }
    }
}